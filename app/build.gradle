apply plugin: 'com.android.application'

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def computeProviderAuthority(applicationId) {
    return "\"" + applicationId + ".databse" + "\""
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.example.ivan.konverzijavaluta"
        minSdkVersion 18
        targetSdkVersion 23
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        resValue "string", "provider_authority", computeProviderAuthority(applicationId)
        buildConfigField "String", "PROVIDER_AUTHORITY", computeProviderAuthority(applicationId)
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Bug - see http://stackoverflow.com/questions/20673625/android-gradle-plugin-0-7-0-duplicate-files-during-packaging-of-apk
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.jakewharton.timber:timber:3.1.0'

    // A debug bridge for Android applications
    compile 'com.facebook.stetho:stetho:1.2.0'
    compile('com.facebook.stetho:stetho-okhttp3:1.3.1') {
        exclude module: 'okhttp'
    }

    // Date time lib
    compile 'net.danlew:android.joda:2.8.2'
}
